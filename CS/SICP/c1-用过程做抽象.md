# 构造过程抽象

## 基本元素

表达式：

```bash
> 486
486

> (+ 1 2)
3

> (- 9 2)
7
> (+ (- 3 2) (+ 4 3))
8
```

命名：

```bash
(define a 10)

> a
10
```

过程：

```bash
(define (square x) (* x x))

> (square 2)
4
```

用lambda表达式定义过程

```bash
(define square (lambda (x) (* x x)))

> (square 2)
4
```

条件表达式

```scheme
(define (abs x)
  (cond
    ((> x 0) x)
    ((= x 0) 0)
    ((< x 0) (- 0 x))))
    
(define (abs-if x)
  (if (< x 0)
      (- 0 x)
      x))
```

Scheme 解释器先对表达式的各个元素求值，然后再代入表达式进行计算。

## 递归

```scheme
(define (factorial x)
  (if (= x 1)
      1
      (* x (factorial (- x 1)))))

(define (factorial-iter x)
  (define (iter n res)
    (if (= n 1)
        res
        (iter (- n 1) (* res n))))
  (iter x 1))
```

## 高阶函数

### 函数作为参数

```scheme
(define (cube x)
  (* x x x))

(define (sum-ints a b)
  (if (> a b)
      0
      (+ a (sum-ints (+ a 1) b))))

(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes (+ a 1) b))))

(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b))))

(define (f-sum a b term next)
  (if (> a b)
      0
      (+ (term a) (f-sum (next a) b term next))))

(define (f-sum-ints a b)
  (f-sum a b (lambda (x) x) inc))

(define (f-sum-cubes a b)
  (f-sum a b cube inc))

(define (f-pi-sum a b)
  (f-sum a
         b
         (lambda (x) (/ 1.0 (* x (+ x 2))))
         (lambda (x) (+ x 4))))
```

### 函数作为一般方法

```scheme
(define (search f neg-point pos-point)
  (define (average a b) (/ (+ a b) 2))
  (define (good? x) (< (- (abs x) 0) 0.001))
  (let ((mid-point (average neg-point pos-point)))
    (cond
      ((good? (f mid-point)) mid-point)
      ((> (f mid-point) 0) (search f neg-point mid-point))
      ((< (f mid-point) 0) (search f mid-point pos-point)))))

(define (fixed-point f first-guess)
  (define tolerance 0.00001)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (if (close-enough? guess (f guess))
        guess
        (try (f guess))))
  (try first-guess))

(define (sqrt x)
  (define (average x y) (/ (+ x y) 2.0))
  (fixed-point
   (lambda (y) (average y (/ x y)))
   2.0))
```

### 函数作为返回值

```scheme
(define (sqrt x)
  (fixed-point
   (average-damp (lambda (y) (/ x y)))
   2.0))

(define (average-damp f)
  (lambda (x) (/ (+ x (f x)) 2)))
```
